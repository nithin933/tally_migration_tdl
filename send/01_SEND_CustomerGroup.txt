;; Shivgoraksh


[Collection: SEND_CustomerGroup]
		
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@MGNHost + '/api/method/express_tally.receive_tally_uae.customer_group'

	RemoteRequest: SEND_CustomerGroup : UTF8
	JSON Object Path: "message:1"
	
;[#Menu: ExpressIntegration]
;	
;;	Add: Item: Send Customer Group : Call: SEND_CustomerGroup
;	Add: Item: Send Customer Group : Call: SEND_CustomerGroup_Start

	[Function: SEND_CustomerGroup_Start]
		
		10 : Start Timer: SEND_CustomerGroup_Timer : 1
		
	[System: Events]
		
		SEND_CustomerGroup_Timer : Timer: True: Call: SEND_CustomerGroup

[Function: SEND_CustomerGroup]

	Variable : vName  : String
	Variable : vMessage : String
	
	Variable: vCounter_send : Number : 1
	Variable: vRetryCount : Number : 0
	Variable: vMaxRetries : Number : 3

	Variable: vStatus : String


	06 : Do If: ($$NumItems:SEND_CustomerCustomer_Tally = 0) :  Stop Timer: SEND_CustomerGroup_Timer
	07 : Do If: ($$NumItems:SEND_CustomerCustomer_Tally = 0) :  Return

	08 : Stop Timer: SEND_CustomerGroup_Timer
	09 : Start Progress: 1 : "Sending Customer Groups" : ""
	10 : Walk Collection: SEND_CustomerGroup
	12 : Set: vStatus : $Status
	13 : Show Progress: 1
	14 : Log: "API Status: " + ##vStatus
	15 : Do If: $$IsEmpty:##vStatus : Log: "ERROR: No API response received"
	16 : Do If: $$IsEmpty:##vStatus : Increment: vRetryCount
	17 : Do If: $$IsEmpty:##vStatus and ##vRetryCount >= ##vMaxRetries : Msg Box: "API Error" : "Failed to connect to MGN after 3 attempts"
	18 : Do If: $$IsEmpty:##vStatus and ##vRetryCount >= ##vMaxRetries : Return
;	20 : 	If: $Status
	22 :	Start Batch Post
	25 : 			Walk Collection: data
	26 : 			Log: $name
	28 : 			Set: vName : $name
	29 : 			Set: vMessage : $message
	30 : 				Modify Object: (Group, ##vName).WebStatus[1].WebStatus : @@MGNResetFlag
	32 :				Modify Object: (Group, ##vName).WebStatus_message[1].WebStatus_message : ##vMessage
	40 :			End Walk
	45 : 	End Batch Post
;	50 : 	End If
	60 : End Walk
	61 : End Progress
;	62 : Stop Timer: SEND_CustomerGroup_Timer 
	65 : Do If: ($$NumItems:SEND_CustomerCustomer_Tally > 0 and Not $$IsEmpty:##vStatus and ##vRetryCount < ##vMaxRetries) :  Start Timer: SEND_CustomerGroup_Timer : 1
	66 : Do If: ##vRetryCount >= ##vMaxRetries : Log: "Customer Group sync stopped after maximum retries"
	

[Report : SEND_CustomerGroup]
	
	Form: SEND_CustomerGroup
	
	[Form: SEND_CustomerGroup]
		
		Part: SEND_CustomerGroup
		Delete: XMLTag

	[Part: SEND_CustomerGroup]
		
		Line: SEND_CustomerGroup
		Repeat: SEND_CustomerGroup : SEND_CustomerCustomer_Tally
		Scroll: Vertical
		
		[Line: SEND_CustomerGroup]
			
			Field: SEND_CustomerGroup, SEND_CustomerGroup_IsGroup, SEND_CustomerGroup_dotype, SEND_CustomerGroup_PrimaryGroup
			JSONTag: "data"
			Empty: $$Line > @@RecordsLimit
			
			[Field: SEND_CustomerGroup]
				
				Set as: $Name
				JSONTag: "customer_group_name"
				
			[Field: SEND_CustomerGroup_IsGroup]
				
				Set as: If $$NumItems:SEND_CustomerGroup_HasNodeColl > 1 then 1 else 0
				JSONTag: "is_group"
				Type: Number
				
					[Collection : SEND_CustomerGroup_HasNodeColl]
						
						Type: Group
						Child Of: $Name
						Belongs To: Yes
				
			[Field: SEND_CustomerGroup_dotype]
				
				Set as: "Customer Group"
				JSONTag: "doctype"

			[Field: SEND_CustomerGroup_PrimaryGroup]
			                                                                                                                                                                                                       	
				Set as: if $depth <= 3 then "All Customer Groups" else $Parent
				JSONTag : "parent_customer_group"


[Collection: SEND_CustomerCustomer_Tally]
	
	Type: Group
	Child Of: "Sundry Debtors"
	Belongs To: Yes
	Sort: @@Default : $depth
	Filter : WebStatus
	
[System: Formula]
	
	WebStatus : $$IsEmpty:$WebStatus or $WebStatus != @@MGNResetFlag

[System: UDF]
	
	WebStatus : String: 31588
	WebStatus_message : String: 31589
	WebStatus_DocName : String: 31590


[#Form: Group]
	
	Option: LedgerWebStatus 

